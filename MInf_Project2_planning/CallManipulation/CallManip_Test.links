import VidAPI;

##Server

fun broadcast(clients, msg) server {
    switch (clients) {
        case [] -> ()
        case clientPid::clients ->
            clientPid ! msg;
            broadcast(clients, msg)
    }
}

fun serverLoop(clients) server {

  receive {

    case Register(clientPid) ->
      serverLoop(clientPid :: clients)

    case Broadcast(msg) ->
      broadcast(clients, msg);
      serverLoop(clients)
    }
}

var serverPid = spawn { serverLoop([]) };

##Client

fun handleNewCallClient() {
  print("new Call Client");
  var id = VidAPI.startClient();
  var idString = "div" ^^ id;
  var divXML =
    <div id = "{idString}">
      <text>{stringToXml(id)}</text>
    </div>;

  appendChildren(divXML, getNodeById("callButtons"));

  serverPid ! Broadcast(NewCaller(self(), id));
  id
}

fun addCallButton(myID, otherID) {
  var buttonXML = if(myID == otherID) {
      <div>
        <button type="submit" l:onclick="{VidAPI.playLocalVid()}">Call {stringToXml(otherID)}</button>
        <button type="submit" l:onclick="{VidAPI.hangupAll(myID)}">Hangup All</button>
        <button type="submit" l:onclick="{VidAPI.setOutgoingAudio(myID, myID, true)}">Re-Enable Audio</button>
        <button type="submit" l:onclick="{VidAPI.setOutgoingVideo(myID, myID, true)}">Re-Enable Video</button>
      </div>
  } else {
      <div>
        <button type="submit" l:onclick="{VidAPI.callClient(myID, otherID)}">Call {stringToXml(otherID)}</button>
        <button type="submit" l:onclick="{VidAPI.hangup(myID, otherID)}">Hangup {stringToXml(otherID)}</button>
        <button type="submit" l:onclick="{VidAPI.setOutgoingAudio(myID, otherID, false)}">Disable Audio</button>
        <button type="submit" l:onclick="{VidAPI.setOutgoingVideo(myID, otherID, false)}">Disable Video</button>
      </div>
  };

  var idString = "div" ^^ myID;
  appendChildren(buttonXML, getNodeById(idString))
}

fun handleOtherCallClient(myIDs, id) {
  print("Other User added " ^^ id);

  var _ = for (myID <- myIDs) {
    addCallButton(myID, id);
    []
  };

  ()

}

fun clientLoop(myIDs, allIDs) {
  receive {

    case NewCallClient() ->
      var id = handleNewCallClient();
      print("Call Client with ID: " ^^ id ^^ " created");
      clientLoop(id :: myIDs, allIDs)

    case NewCaller(source, id) ->
        handleOtherCallClient(myIDs, id);
        clientLoop(myIDs, id :: allIDs)
      }
  }

fun prepClient() {
  serverPid ! Register(self());

  VidAPI.setInputs();
  clientLoop([], [])
}

fun mainPage() {

  var clientPid = spawnClient {prepClient()};

  page
    <html>
      <button type="submit" l:onclick="{clientPid ! NewCallClient()}">Produce Client</button>
      <div id = "callButtons">
      </div>
      <div id = "info">
      </div>
      <div id = "vids"></div>
    </html>
}

fun main() {
    # Spawns a process on the server which keeps track of all clients
    # Registers the "mainPage" route
    addStaticRoute("/js", "js", [("js", "text/javascript")]);
    addRoute("/", fun(_) { mainPage() });
    # Starts the server and distribution
    serveWebsockets();
    servePages()
}


main()
