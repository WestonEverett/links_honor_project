typename ID = Int;
typename Desc = String;
typename Candidates = String;
typename OfferOrAnswer = String;
typename DeviceType = String;
typename DeviceID = String;
typename DeviceLabel = String;
typename MessageType = [| ConnectionRequest | SDP | Ice |];
typename PCMessage = (id : ID, type : MessageType, sdp : String,
                      sdptype : OfferOrAnswer, iceCandidates : Candidates);

module JSWebRTC {
    alien javascript "js/jsWebRTC.js" {
        gatherMediaDeviceIds : (DeviceType) ~> ();
        checkDevicesGathered : (DeviceType) ~> Bool;
        getMediaDeviceIds : (DeviceType) ~> [DeviceID];
        getMediaDeviceLabels : (DeviceType) ~> [DeviceLabel];
        checkIfCameraLoaded : () ~> Bool;
        checkIfMicLoaded : () ~> Bool;
        getCameraReady : (DeviceID) ~> ();
        getMicReady : (DeviceID) ~> ();
        createLocalStreamElement : () ~> ();
        setLocalID : (Int) ~> ();
        getLocalID : () ~> ID;
        setUpPC : (ID) ~> ();
        addStreamToPC : (ID) ~> ();
        connectionInitiatedWithPeer : (ID) ~> Bool;
        setLocalDescForPC : (ID, OfferOrAnswer) ~> ();
        checkIfLocalDescSetForPC : (ID) ~> Bool;
        getLocalDescForPC : (ID) ~> Desc;
        setRemoteDescForPC : (ID, Desc) ~> ();
        checkIfRemoteDescSetForPC : (ID) ~> Bool;
        checkIfConnectedToPeer : (ID) ~> Bool;
        checkIfPCObjectExists : (ID) ~> Bool;
        getBegunIceSearch : () ~> Bool;
        setBegunIceSearch : () ~> ();
        disconnectFromUser : (ID) ~> ();
        collectCandidates : () ~> (Candidates);
        addCandidates : (Candidates, ID) ~> ();
    }
}

fun sendMessage(clients, msg, peerID) server {
  switch (clients) {
    case [] -> ()
    case x::xs ->
      if (first(x) == peerID) {
        second(x) ! msg;
        ()
      } else {
        sendMessage(xs, msg, peerID)
      }
  }
}

fun broadcast(clients, msg) server {
  switch (clients) {
    case [] -> ()
    case x::xs ->
      second(x) ! msg;
      broadcast(xs, msg)
  }
}

fun serverLoop(clients) server {
  receive {
    case Register(pid) ->
      var numberOfClients = length(clients) + 1;
      var newClients = (numberOfClients, pid) :: clients;
      pid ! ID(numberOfClients);
      serverLoop(newClients)

    case Send(msg, peerID) ->
      sendMessage(clients, Message(msg), peerID);
      serverLoop(clients)

    case Broadcast(msg) ->
      broadcast(clients, Message(msg));
      serverLoop(clients)
  }
}

var serverPid = spawn { serverLoop([]) };

fun checkIfConnectedToPeer(peerID) {
  JSWebRTC.checkIfConnectedToPeer(peerID)
}

fun checkIfPCObjectExists(peerID) {
  JSWebRTC.checkIfPCObjectExists(peerID)
}

fun handleIceCandidates() {
  sleep(500);
  var candidates = JSWebRTC.collectCandidates();
  if (candidates <> "No candidates") {
    var localID = JSWebRTC.getLocalID();
    var message = (id=localID, type=Ice, sdp="_", sdptype="_", iceCandidates=candidates);
    serverPid ! Broadcast(message);
    handleIceCandidates()
  } else {
    handleIceCandidates()
  }
}

fun waitUntilLocalDescSetForPC(peerID) {
  var bool = JSWebRTC.checkIfLocalDescSetForPC(peerID);
  if (bool || not(checkIfPCObjectExists(peerID)))
    ()
  else
    waitUntilLocalDescSetForPC(peerID)
}

fun waitUntilRemoteDescSetForPC(peerID) {
  var bool = JSWebRTC.checkIfRemoteDescSetForPC(peerID);
  if (bool || not(checkIfPCObjectExists(peerID)))
    ()
  else
    waitUntilRemoteDescSetForPC(peerID)
}

fun sendDescriptionForPC(peerID, sdpType) {
  if (checkIfPCObjectExists(peerID)) {
    var desc = JSWebRTC.getLocalDescForPC(peerID);
    var localID = JSWebRTC.getLocalID();
    var message = (id=localID, type=SDP, sdp=desc, sdptype=sdpType, iceCandidates="_");
    serverPid ! Send(message, peerID);
    ()
  } else {
    ()
  }
}

fun prepareDescriptionForPC(peerID, sdpType) {
  if (checkIfPCObjectExists(peerID)) {
    JSWebRTC.setLocalDescForPC(peerID, sdpType);
    waitUntilLocalDescSetForPC(peerID)
  } else {
    ()
  }
}

fun setUpNewPeer(peerID, sendOffer) {
  JSWebRTC.setUpPC(peerID);
  if (checkIfPCObjectExists(peerID) && sendOffer) {
    prepareDescriptionForPC(peerID, "offer");
    sendDescriptionForPC(peerID, "offer")
  } else {
    ()
  }
}

fun handleOfferForPC(peerID, remoteDesc) {
  if (checkIfPCObjectExists(peerID)) {
    JSWebRTC.setRemoteDescForPC(peerID, remoteDesc);
    waitUntilRemoteDescSetForPC(peerID);
    prepareDescriptionForPC(peerID, "answer");
    sendDescriptionForPC(peerID, "answer")
  } else {
    ()
  }
}

fun handleMessage(msg) {
  var localID = JSWebRTC.getLocalID();
  var peerID = msg.id;
  switch (msg.type) {
    case ConnectionRequest ->
      if (not(checkIfPCObjectExists(peerID))) {
        setUpNewPeer(peerID, true);
        var begunIceSearch = JSWebRTC.getBegunIceSearch();
        if (begunIceSearch == false) {
          var iceId2 = spawnClient { handleIceCandidates() };
          ()
        } else {
          ()
        }
      } else if (localID > peerID) {
        prepareDescriptionForPC(peerID, "offer");
        sendDescriptionForPC(peerID, "offer");
        ()
      } else {
        ()
      }
    case SDP ->
      if (msg.sdptype == "offer") {
        handleOfferForPC(peerID, msg.sdp)
      } else {
        JSWebRTC.setRemoteDescForPC(peerID, msg.sdp);
        waitUntilRemoteDescSetForPC(peerID)
      }
    case Ice ->
      if (peerID <> localID) {
        JSWebRTC.addCandidates(msg.iceCandidates, peerID)
      } else {
        ()
      }
  }
}

fun waitForDeviceIds(mediatype) {
  var gathered = JSWebRTC.checkDevicesGathered(mediatype);
  if (not(gathered))
    waitForDeviceIds(mediatype)
  else
    ()
}

fun gatherDeviceIds(mediatype) {
  JSWebRTC.gatherMediaDeviceIds(mediatype);
  waitForDeviceIds(mediatype)
}

fun getDeviceIds(mediatype) {
  JSWebRTC.getMediaDeviceIds(mediatype)
}

fun getDeviceLabels(mediatype) {
  JSWebRTC.getMediaDeviceLabels(mediatype)
}

fun waitForCamera() {
  var bool = JSWebRTC.checkIfCameraLoaded();
  if (bool)
    ()
  else
    waitForCamera()
}

fun waitForMic() {
  var bool = JSWebRTC.checkIfMicLoaded();
  if (bool)
    ()
  else
    waitForMic()
}

fun readyMediaDevices(camId, micId) {
  JSWebRTC.getCameraReady(camId);
  waitForCamera();
  JSWebRTC.getMicReady(micId);
  waitForMic();
  JSWebRTC.createLocalStreamElement()
}

# Loop thread run on the client, listens for messages from the server
fun clientLoop() {
  receive {
    case Message(msg) ->
      handleMessage(msg);
      clientLoop()
    case ID(id) ->
      JSWebRTC.setLocalID(id);
      clientLoop()
  }
}

fun clientRegister() {
  serverPid ! Register(self());
  clientLoop()
}

fun connectToUser(peerID) {
  if (not(checkIfConnectedToPeer(peerID))) {
    setUpNewPeer(peerID, false);
    var localID = JSWebRTC.getLocalID();
    var begunIceSearch = JSWebRTC.getBegunIceSearch();
    if (begunIceSearch == false) {
      JSWebRTC.setBegunIceSearch();
      var iceId = spawnClient { handleIceCandidates() };
      var message = (id=localID, type=ConnectionRequest, sdp="_", sdptype="_", iceCandidates="_");
      serverPid ! Send(message, peerID);
      ()
    } else {
      var message = (id=localID, type=ConnectionRequest, sdp="_", sdptype="_", iceCandidates="_");
      serverPid ! Send(message, peerID);
      ()
    }
  } else {
    ()
  }
}

fun disconnectFromUser(peerID) {
  JSWebRTC.disconnectFromUser(peerID)
}

fun getID() {
  var id = JSWebRTC.getLocalID();
  print(intToString(id));
  id
}

fun registerUser() {
  var clientPid = spawnClient { clientRegister() };
  ()
}

fun main() {
    addStaticRoute("/js", "js", [("js", "text/javascript")]);
    # Starts the server and distribution
    serveWebsockets()
}

main()
