module JSFFI {
  alien javascript "/js/vid_calling.js" {
    playLocalVideo : (String) ~> ();
    setName : () ~> ();
    getLocalName : () ~> (String);
    toString : (String) ~> (String);
    getID: () ~> (String);
    setID: () ~> ();

    createOffer: (String) ~> ();
    createAnswer: (String, String) ~> ();
    createAccept: (String, String) ~> ();
    checkAsyncDone: (String) ~> (String);
    hangup: (String) ~> ();
    hangupAll: () ~> ();

    checkForIceCandidates: (String) ~> (String);
    newRemoteCandidate: (String, String) ~> ();
  }
}

fun displayLocalVid() {
  var vidXML = <video id = "local_vid" autoplay = "true" style="object-fit:cover; width:320px; height:240px"></video>;
  appendChildren(vidXML, getNodeById("vids"));

  JSFFI.playLocalVideo("local_vid")
}

fun getSelfInfo() {
  var name = JSFFI.getLocalName();
  var id = JSFFI.getID();

  (clientName = name, clientID = id)
}

##WebRTC Functions

fun waitForAsync(foreignID){

  var response = JSFFI.checkAsyncDone(foreignID);

  if (response <> "wait") {
    response
    }
    else {
      waitForAsync(foreignID)
    }
}


fun checkCandidates(serverPid, foreignID) {
  var candidate = JSFFI.checkForIceCandidates(foreignID);

  if (candidate <> "None") {

    var localID = JSFFI.getID();

    serverPid ! Send(foreignID, VidMessage(senderID = localID, destID = foreignID, msgType = "ice", data = candidate));

    checkCandidates(serverPid, foreignID)
  }
  else {
    checkCandidates(serverPid, foreignID)
  }
}

### CLIENT

##Message Types:
##VidMessage(senderID = id, destID = id, msgType = type, data = data)

fun handleVidMessage(serverPid, msg) {

  var localID = JSFFI.getID();

  var destID = JSFFI.toString(msg.destID);

  var foreignID = JSFFI.toString(msg.senderID);

  var type = JSFFI.toString(msg.msgType);

  print("I am " ^^ localID ^^ " this is for " ^^ destID ^^ " from " ^^ foreignID);

  if(localID <> destID) {
    print("I don't think this is for me")
  }
  else {

    print("I think this is for me");

    switch(type) {
      case "initial" ->
        print("This is an initial VidMessage");
        JSFFI.createOffer(foreignID);
        var offerStr = waitForAsync(foreignID);
        print(offerStr);
        serverPid ! Send(foreignID, VidMessage(senderID = localID, destID = foreignID, msgType = "offer", data = offerStr));

        var iceClient = spawnClient { checkCandidates(serverPid, foreignID) };

        print("I am " ^^ localID ^^ " sending an offer to " ^^ foreignID);
        ()

      case "offer" ->
        print("This is an offer VidMessage");

        var offerStr = JSFFI.toString(msg.data);
        JSFFI.createAnswer(foreignID,offerStr);

        var answerStr = waitForAsync(foreignID);

        serverPid ! Send(foreignID, VidMessage(senderID = localID, destID = foreignID, msgType = "accept", data = answerStr));

        var iceClient = spawnClient { checkCandidates(serverPid, foreignID) };

        print("I am " ^^ localID ^^ " sending an accept to " ^^ foreignID);
        ()

      case "accept" ->
        print("This is an accept VidMessage");
        var answerStr = JSFFI.toString(msg.data);
        JSFFI.createAccept(foreignID,answerStr);

        var _ = waitForAsync(foreignID);

        print("I am " ^^ localID ^^ " accepting an offer from " ^^ foreignID);
        ()

      case "ice" ->
        print("This is an ICE VidMessage");
        var candidate = JSFFI.toString(msg.data);
        JSFFI.newRemoteCandidate(foreignID,candidate);
        ()
    }
  }
}

##changedInfo = clientInfo record
fun moveToRoom(changedInfo){

  var clientID = changedInfo.clientID;
  var clientName = changedInfo.clientName;
  var roomNum = changedInfo.room;

  print("placing " ^^ clientName ^^ " with id " ^^ clientID ^^ " in room " ^^ roomNum);
  var xmlid = "NameOf" ^^ clientID;

  if (roomNum == "0"){
    print("roomNum found to be 0");
    var node = getNodeById(xmlid);

    if(not(isNull(node))) {
      removeNode(node)
    } else {
      ()
    }

  }
  else {
    var node = getNodeById(xmlid);
    if(not(isNull(node))){
      removeNode(node)
    } else {
    ()
    };

    var nameXML = <div id = "{xmlid}"><body>{stringToXml(clientName)}</body></div>;
    appendChildren(nameXML, getNodeById(roomNum));

    ()
  }
}


##StateUpdate((clientInfo(clientID, clientName, room)))

fun handleStateUpdate(serverPid, oldOthersInfo, myInfo, changedInfo) {

  var newOthersInfo = for (c <- oldOthersInfo) {
    if (c.clientID == changedInfo.clientID) {

      if(and([c.room == myInfo.room, myInfo.room <> "0"])){
        JSFFI.hangup(c.clientID);
        print("Hanging up with " ^^ c.clientID)
      }
      else {
      ()
      };

      if(and([changedInfo.room == myInfo.room, myInfo.room <> "0"])) {
        serverPid ! Send(changedInfo.clientID, VidMessage(senderID = myInfo.clientID, destID = changedInfo.clientID, msgType = "initial", data = ""));
        print("Sending Init VidMessage to " ^^ changedInfo.clientID)
      } else {};

      moveToRoom(changedInfo);

      [changedInfo]
    }
    else {
      [c]
    }
  };
  newOthersInfo
}

##InitialState([clientInfo(clientID, clientName, room)])
fun handleInitialState(clientArr) {

  var _ = for (c <- clientArr) {
    moveToRoom(c);
    []
  };
  ()
}

## Loop thread run on the client, listens for NewClient messages from the server
## and prints a message to the console whenever one is received
fun clientLoop(serverPid, othersInfo, myInfo) {
    receive {
        case NewClient(msg) ->
          print("New client registered " ^^ msg.clientID);
          var othersNewInfo = msg :: othersInfo;
          clientLoop(serverPid, othersNewInfo, myInfo)


        case VidMessage(msg) ->
          print("VidMessage Received from " ^^ JSFFI.toString(msg.senderID));
          handleVidMessage(serverPid, msg);
          clientLoop(serverPid, othersInfo, myInfo)

        case StateUpdate(msg) ->
          print("StateUpdate Received");
          var othersNewInfo = handleStateUpdate(serverPid, othersInfo, myInfo, msg);
          clientLoop(serverPid, othersNewInfo, myInfo)

        case InitialState(msg) ->
          print("InitialState received");
          handleInitialState(msg);
          clientLoop(serverPid, msg, myInfo)

        case Migrate(msg) ->
          print("Migrate Received");

          if(myInfo.room <> msg) {

            if(myInfo.room <> "0"){ JSFFI.hangupAll()} else {};

            serverPid ! ChangeRoom(JSFFI.getID(), msg);
            var myNewInfo = (clientID = myInfo.clientID, clientName = myInfo.clientName, room = msg);
            moveToRoom(myNewInfo);
            clientLoop(serverPid, othersInfo, myNewInfo)
          } else {
            clientLoop(serverPid, othersInfo, myInfo)
          }





    }

}

## Run on the client whenever the page has loaded.
## Registers itself with the server, then proceeds to loop, awaiting messages
## from the server
fun clientRegister(serverPid) {

    JSFFI.setName();
    JSFFI.setID();

    var name = JSFFI.getLocalName();
    var id = JSFFI.getID();
    displayLocalVid();

    var clientInfo = (clientID = id, clientName = name, room = "0");

    serverPid ! Register(self(), clientInfo);

    clientLoop(serverPid, [], clientInfo)
}

## Run on the server to generate a page for the client. Spawns a process on the
## client and generates the HTML.

fun mainPage(serverPid) {

  var clientPid = spawnClient { clientRegister(serverPid) };

  page
  <html>
    <body>
    <div id = "1">
      <form l:onsubmit="{clientPid ! Migrate("1")}">
        <button type="submit">Room 1</button>
      </form>
    </div>
     <div id = "2">
      <form l:onsubmit="{clientPid ! Migrate("2")}">
        <button type="submit">Room 2</button>
      </form>
    </div>
    <div id = "vids"></div>

    </body>
  </html>

}

### SERVER
## Sends a message to a list of clients
fun broadcast(clients, msg) server {
    switch (clients) {
        case [] -> ()
        case (c,_)::clients ->
            c ! msg;
            broadcast(clients, msg)
    }
}

# Loops, keeping track of the client PIDs
fun serverLoop(clients) server {
    receive {
        case Register(pid, clientInfo) ->
            var newClients = (pid, clientInfo) :: clients;

            # Broadcast a "NewClient" message
            broadcast(clients, NewClient(clientInfo));

            var infoToSend = for ((_ , clientInfo) <- newClients){
              [clientInfo]
            };

            broadcast([(pid, clientInfo)], InitialState(infoToSend));

            serverLoop(newClients)

        case Send(targetID, msg) ->

          var target = for ((pid , clientInfo) <- clients)
            if (clientInfo.clientID == targetID) {
              [(pid , clientInfo)]
            } else {
              []
            };

            broadcast(target, msg);
            serverLoop(clients)

        case Broadcast(msg) ->
          broadcast(clients, msg);
          serverLoop(clients)

        case ChangeRoom(userID, newRoom) ->

          var newClients = for ((pid , clientInfo) <- clients)
            if (clientInfo.clientID == userID) {
              var id = clientInfo.clientID;
              var name = clientInfo.clientName;
              var newClientInfo = (clientID = id, clientName = name, room = newRoom);

              [(pid, newClientInfo)]
            }

            else
              [(pid, clientInfo)];

          var clientsToUpdate = for ((pid , clientInfo) <- newClients)
            if (clientInfo.clientID <> userID) {
              [(pid , clientInfo)]
            }
            else
              [];

          var clientInfoToSend = for ((pid , clientInfo) <- newClients)
            if (clientInfo.clientID == userID) {
              [clientInfo]
            }
            else
              [];

          var infoToSend = hd(clientInfoToSend);

          broadcast(clientsToUpdate, StateUpdate(infoToSend));

          serverLoop(newClients)

    }
}


# Main method, run once when the server is started
fun main() {
    # Spawns a process on the server which keeps track of all clients
    var serverPid = spawn { serverLoop([]) };
    # Registers the "mainPage" route
    addRoute("/", fun(_) { mainPage(serverPid) });
    addStaticRoute("/js", "js", [("js", "text/javascript")]);
    # Starts the server and distribution
    serveWebsockets();
    servePages()
}


main()
